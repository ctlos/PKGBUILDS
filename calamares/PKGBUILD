# Ctlos Calamares
pkgname=calamares-dev
pkgver=3.3.13
_pkgver=3.3.13
pkgrel=6
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
url="https://github.com/ctlos/calamares"
license=('LGPL')
conflicts=('calamares' 'calamares-net')

depends=(
  'boost-libs'
  'ckbcomp'
  'cryptsetup'
  'doxygen'
  'efibootmgr'
  'gptfdisk'
  'gtk-update-icon-cache'
  'icu'
  'kconfig'
  'kcoreaddons'
  'kcrash'
  'ki18n'
  'kparts'
  'kpmcore'
  'kservice'
  'kwidgetsaddons'
  'libpwquality'
  'mkinitcpio-openswap'
  'polkit-qt6'
  'rsync'
  'qt6-declarative'
  'solid'
  'squashfs-tools'
  'yaml-cpp'
  )

makedepends=(
  'boost'
  'cmake'
  'extra-cmake-modules'
  'git'
  'ninja'
  'python-jsonschema'
  'python-pyaml'
  'python-unidecode'
  'qt6-tools'
)

backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source+=("https://github.com/calamares/calamares/releases/download/v${_pkgver}/calamares-${_pkgver}.tar.gz"
        git+https://github.com/ctlos/calamares#branch=dev)

sha256sums=('SKIP'
            'SKIP')

prepare() {
    cp -a ${srcdir}/calamares/* ${srcdir}/calamares-${_pkgver}
    cp -rf ${srcdir}/calamares/settings-offline.conf ${srcdir}/calamares-${_pkgver}/settings.conf
    cp -rf ${srcdir}/calamares/src/modules/packages/packages-offline.conf ${srcdir}/calamares-${_pkgver}/src/modules/packages/packages.conf
    cp -rf ${srcdir}/calamares/src/modules/welcome/welcome-offline.conf ${srcdir}/calamares-${_pkgver}/src/modules/welcome/welcome.conf

    rm -rf ${srcdir}/calamares
    cd ${srcdir}/calamares-${_pkgver}

    sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' CMakeLists.txt
    sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 4  \# 2048MiB/" "src/modules/fstab/main.py"

    # fix sddm autologin in 3.3.5
    # sed -i -e 's|configuration_file = .*|configuration_file = "/etc/sddm.conf"|g' "src/modules/displaymanager/main.py"
    # sed -i -e "s|self.configuration_file|self.configuration_file.lstrip('/')|g"   "src/modules/displaymanager/main.py"

    # change branding
    sed -i -e "s/default/ctlos/g" src/branding/CMakeLists.txt
}

build() {
    cd ${srcdir}/calamares-${_pkgver}

    # export CXXFLAGS+=" -fPIC"

    cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DWITH_APPSTREAM=OFF \
        -DWITH_PYBIND11=OFF \
        -DWITH_QT6=ON \
        -DSKIP_MODULES="dracut dracutlukscfg \
        dummycpp dummyprocess dummypython dummypythonqt \
        finishedq keyboardq license localeq notesqml oemid \
        openrcdmcryptcfg packagechooserq fsresizer \
        rawfs mkinitfs contextualprocess interactiveterminal \
        plasmalnf services-openrc \
        summaryq tracking usersq webview welcomeq"

    cmake --build build --parallel 18
}

package() {
    cd ${srcdir}/calamares-${_pkgver}/build

    DESTDIR="${pkgdir}" cmake --build . --target install

    install -Dm644 "../data/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
    install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
    install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
    chmod 750 "$pkgdir"/etc/polkit-1/rules.d

    cp -r ../settings-{on,off}line.conf $pkgdir/usr/share/calamares/
    cp -r ../src/modules/packages/packages-{on,off}line.conf $pkgdir/usr/share/calamares/modules/
    cp -r ../src/modules/welcome/welcome-{on,off}line.conf $pkgdir/usr/share/calamares/modules/
    cp -r ../src/modules/netinstall/netinstall.yaml $pkgdir/usr/share/calamares/modules/netinstall.yaml
}
